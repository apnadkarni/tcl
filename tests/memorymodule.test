if {"::tcltest" ni [namespace children]} {
    package require tcltest 2.5
    namespace import -force ::tcltest::*
}

testConstraint memorymoduletest [expr {0 == [catch {zipfs mount dltest/memorymoduletest.zip [file join [zipfs root] memorymoduletest]}]}]

lappend auto_path [file join [zipfs root] memorymoduletest]

testConstraint thread [expr {[catch {package require thread 3.0}] == 0}]
testConstraint memorymodule [expr {[tcl::build-info memorymodule] != 0}]
testConstraint nestedexception [expr {[tcl::build-info msvc] != 0}]
testConstraint tls_supported 0; #For now, TLS in dll's is not supported

test memorymodule-1.0 {info loaded} memorymoduletest {
    package require memorymoduletest
    info loaded {} Memorymoduletest
} [file join [zipfs root] memorymoduletest/tcl9memorymoduletest.dll]

test memorymodule-1.1 {GetModuleFileNameA (WIP)} {memorymoduletest memorymodule} {
    package require memorymoduletest
    GetModuleFileNameA
} //zipfs:/memorymoduletest/tcl9memorymoduletest.dll

test memorymodule-1.2 {GetModuleFileNameW (WIP)} {memorymoduletest memorymodule} {
    package require memorymoduletest
    GetModuleFileNameW
} //zipfs:/memorymoduletest/tcl9memorymoduletest.dll

test memorymodule-2.0 {LTS, create thread before accessing var} -constraints {memorymoduletest thread tls_supported} -body {
    package require Thread
    package require memorymoduletest
    set t1 [thread::create]
    ThreadVar 15; # Set ThreadVar to 15 in the main thread
    thread::preserve $t1
    thread::send $t1 {
	lappend auto_path [file join [zipfs root] memorymoduletest]
	package require memorymoduletest
	# set ThreadVar to 16 in the subthread
	ThreadVar 16
	return [ThreadVar]
    } result
    thread::release $t1
    # ThreadVar in main thread should be unchanged (15)
    list [ThreadVar] $result
} -result {15 16}

test memorymodule-2.1 {LTS, create thread after accessing var} -constraints {memorymoduletest thread tls_supported} -body {
    package require Thread
    package require memorymoduletest
    ThreadVar 15; # Set ThreadVar to 15 in the main thread
    set t1 [thread::create]; #create thread _before_ setting the variable
    thread::preserve $t1
    thread::send $t1 {
	lappend auto_path [file join [zipfs root] memorymoduletest]
	package require memorymoduletest
	# set ThreadVar to 16 in the subthread
	ThreadVar 16
	return [ThreadVar]
    } result
    thread::release $t1
    # ThreadVar in main thread should be unchanged (15)
    list [ThreadVar] $result
} -result {15 16}

test memorymodule-2.2 {nexted exception} -constraints {memorymoduletest nestedexception} -body {
    NestedException
} -result 1

test memorymodule-3.0 {_tls_index} -constraints {memorymoduletest thread tls_supported} -body {
    set t1 [thread::create];
    ThreadVar 18; # Set ThreadVar to 18 in the main thread
    thread::preserve $t1
    thread::send $t1 {
	lappend auto_path [file join [zipfs root] memorymoduletest]
	package require memorymoduletest
	ThreadVar 19; # Set ThreadVar to 18 in the main thread
	return [list [ThreadVar -index] [ThreadVar -start] [ThreadVar -end]]
    } result
    thread::release $t1
    lappend result [ThreadVar -index] [ThreadVar -start] [ThreadVar -end]
} -result {6 0 0 6 0 0}

# cleanup
::tcltest::cleanupTests
return

